/*
 * Copyright 2016 Martin Kamp Jensen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

apply from: rootProject.file('signing.gradle')

android {

  compileSdkVersion 28
  buildToolsVersion "28.0.3"

  defaultConfig {

    applicationId "com.github.mkjensen.tv"
    minSdkVersion 21
    targetSdkVersion 28
    versionCode 26
    versionName "0.0.26"
  }

  compileOptions {

    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {

    debug {

      applicationIdSuffix ".debug"
      minifyEnabled false
      versionNameSuffix '-debug'
    }

    release {

      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  dexOptions {

    // Travis CI: https://github.com/codepath/android_guides/wiki/Setting-up-Travis-CI
    preDexLibraries = preDexEnabled && !travisBuild
  }

  packagingOptions {

    // Avoid build errors like: Error:Execution failed for task ':app:transformResourcesWithMergeJavaResForDebug'. > More than one file was found with OS independent path 'META-INF/android.arch.lifecycle_runtime.version'
    exclude 'META-INF/android.arch.lifecycle_runtime.version'
    exclude 'META-INF/com.android.support_support-compat.version'
    exclude 'META-INF/com.android.support_support-core-ui.version'
    exclude 'META-INF/com.android.support_support-core-utils.version'
    exclude 'META-INF/com.android.support_support-fragment.version'
    exclude 'META-INF/com.android.support_support-media-compat.version'
    exclude 'META-INF/com.android.support_support-v4.version'
  }
}

ext {

  androidSupportVersion = "28.0.0"
  autoValueVersion = "1.6.2"
  autoValueMoshiVersion = "0.4.5"
  crashlyticsVersion = "2.9.5"
  companionlibraryVersion = "0.4.1"
  daggerVersion = "2.17"
  exoplayerVersion = "2.8.4"
  firebaseCoreVersion = "16.0.3"
  licensesDialogVersion = "1.8.3"
  moshiVersion = "1.6.0"
  okhttpVersion = "3.11.0"
  retrofitVersion = "2.4.0"
  timberVersion = "4.7.1"
}

dependencies {

  annotationProcessor "com.google.auto.value:auto-value:$autoValueVersion"
  annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
  annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
  annotationProcessor "com.ryanharter.auto.value:auto-value-moshi:$autoValueMoshiVersion"

  // Workaround for https://github.com/rharter/auto-value-moshi/issues/106
  annotationProcessor "com.google.auto.value:auto-value-annotations:$autoValueVersion"

  compileOnly "com.google.auto.value:auto-value-annotations:$autoValueVersion"
  compileOnly "com.ryanharter.auto.value:auto-value-moshi-annotations:$autoValueMoshiVersion"

  implementation "com.android.support:leanback-v17:$androidSupportVersion"
  implementation "com.android.support:support-annotations:$androidSupportVersion"
  implementation "com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion"
  implementation "com.google.android.exoplayer:exoplayer-core:$exoplayerVersion"
  implementation "com.google.android.exoplayer:exoplayer-hls:$exoplayerVersion"
  implementation "com.google.android.exoplayer:extension-okhttp:$exoplayerVersion"
  implementation("com.google.android.libraries.tv:companionlibrary:$companionlibraryVersion") {
    exclude group: "com.android.support"
    exclude group: "com.google.ads.interactivemedia.v3"
    exclude group: "com.google.android.gms"
  }
  implementation "com.google.dagger:dagger:$daggerVersion"
  implementation "com.google.dagger:dagger-android:$daggerVersion"
  implementation "com.google.dagger:dagger-android-support:$daggerVersion"
  implementation "com.google.firebase:firebase-core:$firebaseCoreVersion"
  implementation "com.jakewharton.timber:timber:$timberVersion"
  implementation "com.squareup.moshi:moshi:$moshiVersion"
  implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
  implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
  implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
  implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
  implementation "de.psdev.licensesdialog:licensesdialog:$licensesDialogVersion"

  // Overwrite other dependencies to avoid bugs
  implementation "com.android.support:appcompat-v7:$androidSupportVersion"
  implementation "com.android.support:support-v4:$androidSupportVersion"
}

apply plugin: 'com.google.gms.google-services'
